plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

apply from: '../apps.gradle'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.is.ExbinIs'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.mainClass = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (!hasProperty('mainClass')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    flatDir {
        dirs "../../lib"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation moduleDep('ExbinIsJava', 'org.exbin.is', 'exbin-is-framework')
    implementation moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-core')
    implementation moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-catalog')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-frame-api')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-frame')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-action')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-file')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-editor')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-about')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-help')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-operation-undo')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-data')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-options')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-editor-xbup')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-editor-text')
    implementation 'commons-cli:commons-cli:1.2'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.6.2'
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    runtimeOnly 'org.eclipse.persistence:javax.persistence:2.2.1'
    runtimeOnly 'org.eclipse.persistence:eclipselink:2.7.7'
    testImplementation group: 'junit', name: 'junit', version: '4.10'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { '../lib/' + it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}
