apply from: '../tools.gradle'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.is.service.ExbinService'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (!hasProperty('mainClass')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    compile moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-core')
    compile moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-client')
    compile moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-service')
    compile moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-api')
    compile moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-service-data')
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.eclipse.persistence:eclipselink:2.5.1'
    runtime 'mysql:mysql-connector-java:5.1.30'
    runtime 'org.eclipse.persistence:javax.persistence:2.1.0'
    runtime 'org.apache.derby:derbyclient:10.12.1.1'
    runtime 'org.apache.derby:derby:10.12.1.1'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

def manifestClassPath() {
    try {
        return (configurations.runtime.collect { '../lib/' + it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

